plugins {
    id "dev.architectury.loom" version "1.0-SNAPSHOT"
    id "maven-publish"
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

loom {
    silentMojangMappingsLicense()

    accessWidenerPath = file("src/main/resources/questsgiver.accesswidener")

    forge {
        dataGen {
            mod project.mod_id
        }
    }
}

repositories {
    maven { url "https://maven.theillusivec4.top/" }
    maven { url "https://maven.melanx.de" }
    maven { url "https://www.jitpack.io" }
    maven { url "https://cursemaven.com" }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.officialMojangMappings()
    forge "net.minecraftforge:forge:${project.forge_version}"

    modImplementation(annotationProcessor("io.github.noeppi_noeppi.mods:LibX:1.16.3-1.0.80"))

    //modImplementation("curse.maven:Reputation-579116:4028368")
    modImplementation("curse.maven:MoreVillagers-484954:3466080")
    modImplementation("curse.maven:GuardVillagers-360203:3449429")
    modImplementation("com.github.Lupicus:Beekeeper:1.16.2-SNAPSHOT")
    modImplementation("curse.maven:EpicFight-405076:3805345")
    modImplementation("curse.maven:AtlasLib-463826:3541116")
    modImplementation("curse.maven:FollowMe-463841:3804364")
}

processResources {
    inputs.property "version", project.version

    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    options.release = 8
}

java {
    withSourcesJar()
}

jar {
    // add some additional metadata to the jar manifest
    manifest {
        attributes([
                "Specification-Title"     : project.mod_id,
                "Specification-Vendor"    : project.mod_author,
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : version,
                "Implementation-Vendor"   : project.mod_author,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
			from components.java
		}
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}
